import functions as fc;
import mouseTracking as mt;
import Battle.battleStart as bbs;
import Interface.userGUI as iug;
import Interface.menu_Bag as imb;
import Interface.menu_Pokemon as imp;
import Player.playerSetting as pps;
import Pokemon.myPokemon as pmp;
import Pokemon.pokemonInfo as ppi;
import Pokemon.weakRelation as pwr;
import Pokemon.calculateMethod as pcm;
import Pokemon.pokemonStatus as ppst;
import Variables.mainSentence as vms;
import Variables.etcData as ved;

const t = StringBuffer(1024);
const b = StringBuffer(1024);
var preScreenSizeX  = 0;
var wlen            = 0;
const pokemonCurHP  = PVariable();
const hpread_epd    = py_eval('f_readgen_epd(0xFFFFFF00, (0, lambda x: x>>8))');

function selectBattleGUI(cp, index);
function battleMenuGUI(cp);
function battleTextGUI(cp){
    mt.checkScreen();
    if(preScreenSizeX != mt.screenSizeX){
        preScreenSizeX = mt.screenSizeX;
        wlen = (mt.screenSizeX-mt.screenWidth) / 20;
    }
    if(MemoryXEPD(bbs.battleEPD[cp]+0x4C/4, AtLeast, 0x100, 0xFF00)){
        pokemonCurHP[cp] = hpread_epd(bbs.battleEPD[cp]+2);
        if(pokemonCurHP[cp] < 40 && ved.sound_HPDelay[cp] == 0){ved.sound_HPDelay[cp] = 40; PlayWAV("staredit\\wav\\emerald_low hp.wav");}
    }
    else{pokemonCurHP[cp] = 0;}
    if(MemoryXEPD(bbs.battleEPD[cp+4]+0x4C/4, AtLeast, 0x100, 0xFF00)){pokemonCurHP[cp+4] = hpread_epd(bbs.battleEPD[cp+4]+2);}
    else{pokemonCurHP[cp+4] = 0;}

    //배틀메뉴 표시
    const sub = iug.subMenu[cp];
    eprintf("{:s}",vms.stickText);
    if(sub == 0){
        b.insert(0);
        b.appendf("{:s}\x1E{:s}  \x04Lv.\x17{}\n",pps.blankText[wlen],bbs.pokemonName[cp+4],bbs.pokemonLevel[cp+4]);
        b.appendf("{:s}\x08HP \x04{}/{}\n\n\n\n\n\n",pps.blankText[wlen],pokemonCurHP[cp+4],bbs.pokemonHP[cp+4]);
        b.appendf("\x12\x1E{:s}  \x04Lv.\x17{}{:s}\n",bbs.pokemonName[cp],bbs.pokemonLevel[cp],pps.blankText[wlen]);
        b.appendf("\x12\x08HP \x04{}/{}{:s}",pokemonCurHP[cp],bbs.pokemonHP[cp],pps.blankText[wlen]);
        b.DisplayAt(0);
        battleMenuGUI(cp);
    }
    else if(sub == 2){imb.subMenu_Book(cp, 0);} //가방
    else if(sub == 3){imp.subMenu_Pokemon(cp, 0, 1);} //포켓몬
    imp.changePokemon(cp); //포켓몬 교체함수
}


function battleMenuGUI(cp){
    const index = pps.selectMenu[cp];
    t.insert(0, "\n");
    t.appendf("\t\t\x17{:s}\x04은(는) 무엇을 할까?\n", bbs.pokemonName[cp]);
    t.appendf("\t\t\t{:s}\x04가방\n", pps.selectIcon1[index]);
    t.appendf("\t\t\t{:s}\x04포켓몬\n", pps.selectIcon2[index]);
    t.appendf("\t\t\t{:s}\x04도망간다", pps.selectIcon3[index]);
    t.DisplayAt(2);
    selectBattleGUI(cp, index);
}

function selectBattleGUI(cp, index){
    if(Deaths(cp, Exactly, 1, "A Stick")){
        if(index == 0){pps.selectMenu[cp] = 2;}
        else{pps.selectMenu[cp] -= 1;}
        PlayWAV("staredit\\wav\\GUI sel cursor.wav");
        pps.guiUpdate[cp] = 1;
    }
    if(Deaths(cp, Exactly, 2, "A Stick")){
        if(index == 2){pps.selectMenu[cp] = 0;}
        else{pps.selectMenu[cp] += 1;}
        PlayWAV("staredit\\wav\\GUI sel cursor.wav");
        pps.guiUpdate[cp] = 1;
    }
    if(Deaths(cp, Exactly, 8, "A Stick")){
        if(index == 0){
            iug.subMenu[cp] = 2;
            pps.guiUpdate[cp] = 1;
            vms.removeTextAll();
        }
        else if(index == 1){
            iug.subMenu[cp] = 3;
            pps.guiUpdate[cp] = 1;
            vms.removeTextAll();
        }
        else if(index == 2){

        }
        PlayWAV("staredit\\wav\\se_gui_misc6.wav");
    }
}


/*
메뉴
가방
포켓몬
도망간다
*/

