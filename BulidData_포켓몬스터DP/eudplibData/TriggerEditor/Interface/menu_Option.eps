import Interface.userGUI as iug;
import Player.playerSetting as pps;
import Variables.mainSentence as vms;
import Variables.restrictSentence as vrs;
import Variables.subSentence as vus;
import SCA.scaSystem as scs;


const t = StringBuffer(1024);
const optionIndex   = PVariable();

function subMenu_OptionAction(cp, index);
function subMenu_Option(cp, height){
    const index = optionIndex[cp];
    if(pps.guiUpdate[cp]){
        pps.guiUpdate[cp] = 0;
        t.insert(0);
        t.appendf("\t\t\x1D옵션\n\n");

        t.appendf("\t\t{:s}\x04저장하기 (\x1E{}슬롯\x04)\n",pps.selectIcon1[index],scs.userSlot[cp]+1);
        t.appendf("\t\t{:s}\x04배경음악 ({:s}\x04)\n",pps.selectIcon2[index],pps.switchIcon[pps.setMusic[cp]]);
        t.appendf("\t\t{:s}\x04화면고정 ({:s}\x04)\n",pps.selectIcon3[index],pps.switchIcon[pps.setScreen[cp]]);
        t.appendf("\t\t{:s}\x04오토배틀 ({:s}\x04)\n",pps.selectIcon4[index],pps.switchIcon[pps.setAutoBattle[cp]]);
        t.appendf("\t\t{:s}\x04없음\n",pps.selectIcon5[index]);
        t.appendf("\t\t{:s}\x04없음\n",pps.selectIcon6[index]);
    }
    t.DisplayAt(height);
    eprintf("{:s}",vms.stickText);
    subMenu_OptionAction(cp, index);
}

function isSaveState(cp){
    if(vms.actionStep[cp] || vrs.restrictIndex[cp] || vus.subIndex[cp]){
        DisplayText("\x08！\x04대화중일 때는 저장할 수 없습니다.");
        return false;
    }
    if(pps.battleState[cp]){
        DisplayText("\x08！\x04전투중일 때는 저장할 수 없습니다.");
        return false;
    }
    if(scs.saveCode[cp]){
        return false;
    }
    return true;
}

function subMenu_OptionAction(cp, index){
    if(Deaths(cp, Exactly, 1, "A Stick")){
        if(index == 0){optionIndex[cp] = 5;}
        else{optionIndex[cp] -= 1;}
        PlayWAV("staredit\\wav\\GUI sel cursor.wav");
        pps.guiUpdate[cp] = 1;
    }
    if(Deaths(cp, Exactly, 2, "A Stick")){
        if(index == 5){optionIndex[cp] = 0;}
        else{optionIndex[cp] += 1;}
        PlayWAV("staredit\\wav\\GUI sel cursor.wav");
        pps.guiUpdate[cp] = 1;
    }
    //뒤로가기
    if(Deaths(cp, Exactly, 4, "A Stick")){
        iug.subMenu[cp] = 0;
        pps.guiUpdate[cp] = 1;
        PlayWAV("staredit\\wav\\se_gui_misc5.wav");
    }
    //옵션설정
    if(Deaths(cp, Exactly, 8, "A Stick")){
        if(index == 0){
            if(isSaveState(cp)){scs.saveCode[cp] = 1;}
            PlayWAV("staredit\\wav\\PC Login.wav");
        }
        else if(index == 1){
            pps.setMusic[cp] = !pps.setMusic[cp];
            PlayWAV("staredit\\wav\\se_gui_misc6.wav");
        }
        else if(index == 2){
            pps.setScreen[cp] = !pps.setScreen[cp];
            PlayWAV("staredit\\wav\\se_gui_misc6.wav");
        }
        else if(index == 3){
            pps.setAutoBattle[cp] = !pps.setAutoBattle[cp];
            PlayWAV("staredit\\wav\\se_gui_misc6.wav");
        }
        pps.guiUpdate[cp] = 1;
    }
}