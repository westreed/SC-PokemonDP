import Battle.battleStart as bbs;
import Battle.battleGUI as bbg;
import Interface.menu_Bag as imb;
import Interface.userGUI as iug;
import Item.itemInfo as iif;
import Pokemon.pokemonInfo as ppi;
import Pokemon.myPokemon as pmp;
import Pokemon.calculateMethod as pcm;
import Pokemon.weakRelation as pwr;
import Pokemon.pokemonStatus as ppst;
import Player.playerSetting as pps;
import Variables.locationInfo as vli;
import Variables.mainSentence as vms;
import dataTable as dT;
import functions as fc;

const s = StringBuffer(1024);
const t = StringBuffer(1024);
const pokeIndex     = PVariable();
const pokePage      = PVariable(); //내포켓몬정보 페이지
const pokeAction    = PVariable(); //포켓몬 GUI에서 다른 액션을 취해야할 경우
const pokeChange    = PVariable(); //포켓몬 교체
const changeName    = PVariable();
/*
pokeAction
0:가방
1:배틀 (가방아이템)
2:위치교환
*/

/*
고려해야할 것들

배틀 상황에서 포켓몬 교체를 하려는 경우
가방에서 포켓몬대상 아이템을 사용하려는 경우 (치료약, 지니게할 아이템 등등)
*/


function subMenu_PokemonAction(cp, index);
function subMenu_PokemonBattleAction(cp, index);
function subMenu_PokemonInfo(cp, height);
function subMenu_Pokemon(cp, height, battle){
    const action = pokeAction[cp];
    if(action || iug.subMiniMenu[cp] == 0){
        const index = pokeIndex[cp];
        if(pps.guiUpdate[cp]){
            pps.guiUpdate[cp] = 0;
            const cpPk = cp*pmp.userPokemonNum;
            t.insert(0);
            if(battle == 1){t.appendf("\t\t\x1D어떤 포켓몬으로 교체할까요?\n\n");}
            else if(action == 0){t.appendf("\t\t\x1D포켓몬\n\n");}
            else if(action == 1){t.appendf("\t\t\x1D누구에게 사용할까요?\n\n");}
            else if(action == 2){t.appendf("\t\t\x1D어떤 포켓몬과 위치를 교체할까요?\n\n");}

            for(var i=0; i<6; i++){
                const mpk = pmp.myPokemon.cast(pmp.playerPokemonList[cpPk+i]);
                const icon = EUDArray.cast(pps.selectIconList[i]);
                if(mpk.index){
                    const mpkn = pmp.myPokemon2.cast(mpk.next);
                    const pk = ppi.Pokemon1.cast(ppi.pokemonList[mpk.index-1]);
                    //전투중일 땐 HP가 계속 변화하므로, HP표기를 안함
                    if(pps.battleState[cp]){
                        t.appendf("\t\t{:s}\x17Lv.{} \x04{:s}",icon[index],mpk.level,pk.name);
                        if(mpkn.battle == 1){t.appendf("\x08(전투)\n");}
                        else if(mpk.state == 1){t.appendf("\x1E(기절)\n");}
                        else{t.appendf("\n");}
                    }
                    else{
                        if(mpk.state == 0){
                            const BTs_HP = pcm.returnStatsHP(pk.BTs);
                            const IVs_HP = pcm.returnIVsHP(mpk.IVs);
                            const maxHP = pcm.returnHitPoints(cp, mpk.level, BTs_HP, IVs_HP);
                            t.appendf("\t\t{:s}\x17Lv.{} \x04{:s} \x1E{}/{}\n",icon[index], mpk.level, pk.name, mpk.hitpoint, maxHP);
                        }
                        else{t.appendf("\t\t{:s}\x17Lv.{} \x04{:s} \x1E(\x08기절\x1E)\n",icon[index], mpk.level, pk.name);}
                    }
                }else{t.appendf("\t\t{:s}\x04없음\n",icon[index]);}
            }
        }
        t.DisplayAt(height);
        eprintf("{:s}",vms.stickText);
        if(battle == 1){subMenu_PokemonBattleAction(cp, index);}
        else if(action == 0 || action == 2){subMenu_PokemonAction(cp, index);}
    }
    else{subMenu_PokemonInfo(cp, height);}
}

function subMenu_PokemonAction(cp, index){
    if(Deaths(cp, Exactly, 1, "A Stick")){
        if(index == 0){pokeIndex[cp] = 5;}
        else{pokeIndex[cp] -= 1;}
        PlayWAV("staredit\\wav\\GUI sel cursor.wav");
        pps.guiUpdate[cp] = 1;
    }
    if(Deaths(cp, Exactly, 2, "A Stick")){
        if(index == 5){pokeIndex[cp] = 0;}
        else{pokeIndex[cp] += 1;}
        PlayWAV("staredit\\wav\\GUI sel cursor.wav");
        pps.guiUpdate[cp] = 1;
    }
    //뒤로가기
    if(Deaths(cp, Exactly, 4, "A Stick")){
        //위치교체 상태
        if(pokeAction[cp] == 2){pokeAction[cp] = 0;}
        //포켓몬 메뉴
        else{iug.subMenu[cp] = 0;}
        pps.guiUpdate[cp] = 1;
        PlayWAV("staredit\\wav\\se_gui_misc5.wav");
    }
    //결정
    if(Deaths(cp, Exactly, 8, "A Stick")){
        //위치를 교체할 대상 선택
        if(pokeAction[cp] == 2){
            const preIndex  = iug.subMiniMenu[cp];
            const nextIndex = index+1;
            if(preIndex != nextIndex){
                const cpPk      = cp*pmp.userPokemonNum;
                const preMpk    = pmp.myPokemon.cast(pmp.playerPokemonList[cpPk+preIndex-1]);
                const prePk     = ppi.Pokemon1.cast(ppi.pokemonList[preMpk.index-1]);
                const nextMpk   = pmp.myPokemon.cast(pmp.playerPokemonList[cpPk+nextIndex-1]);
                const nextPk    = ppi.Pokemon1.cast(ppi.pokemonList[nextMpk.index-1]);
                s.printf("\x07！\x1ELv.{} {:s}\x04와(과) \x1ELv.{} {:s}\x04의 위치를 교체합니다.",preMpk.level,prePk.name,nextMpk.level,nextPk.name);

                const tempMpk   = pmp.myPokemon.alloc();
                preMpk.copyto(tempMpk); //preMpk  -> tempMpk
                nextMpk.copyto(preMpk); //nextMpk -> preMpk
                tempMpk.copyto(nextMpk);//tempMpk -> nextMpk
                pmp.myPokemon.free(tempMpk);

                iug.subMiniMenu[cp] = 0; pokeAction[cp] = 0;
                pps.guiUpdate[cp] = 1;
                
                PlayWAV("staredit\\wav\\se_gui_misc6.wav");
            }
            else{
                DisplayText("\x08！\x04같은 위치의 포켓몬과 교체할 수 없습니다.");
                PlayWAV("staredit\\wav\\se_gui_misc5.wav");
            }
        }
        //내 포켓몬 정보 보기
        else{
            const cpPk = cp*pmp.userPokemonNum;
            const mpk = pmp.myPokemon.cast(pmp.playerPokemonList[cpPk+index]);
            if(mpk.index > 0){
                pokePage[cp] = 0; //첫번째 페이지부터
                iug.subMiniMenu[cp] = index+1;
                pps.guiUpdate[cp] = 1;
                PlayWAV("staredit\\wav\\se_gui_misc6.wav");
            }
        }
    }
}

function subMenu_PokemonBattleAction(cp, index){
    if(Deaths(cp, Exactly, 1, "A Stick")){
        if(index == 0){pokeIndex[cp] = 5;}
        else{pokeIndex[cp] -= 1;}
        PlayWAV("staredit\\wav\\GUI sel cursor.wav");
        pps.guiUpdate[cp] = 1;
    }
    if(Deaths(cp, Exactly, 2, "A Stick")){
        if(index == 5){pokeIndex[cp] = 0;}
        else{pokeIndex[cp] += 1;}
        PlayWAV("staredit\\wav\\GUI sel cursor.wav");
        pps.guiUpdate[cp] = 1;
    }
    //뒤로가기
    if(Deaths(cp, Exactly, 4, "A Stick")){
        if(bbs.battleStep[cp] != 7){
            iug.subMenu[cp] = 0;
            pps.guiUpdate[cp] = 1;
            PlayWAV("staredit\\wav\\se_gui_misc5.wav");
        }
    }
    //포켓몬 교체
    if(Deaths(cp, Exactly, 8, "A Stick") && bbs.pokemonNumber[cp] != index){
        const cpPk = cp*pmp.userPokemonNum;
        const mpk = pmp.myPokemon.cast(pmp.playerPokemonList[cpPk+index]);
        if(mpk.index > 0 && mpk.state == 0){
            const pk = ppi.Pokemon1.cast(ppi.pokemonList[mpk.index-1]);
            changeName[cp] = pk.name;
            pokeChange[cp] = 1;
            iug.subMiniMenu[cp] = index+1;
            iug.subMenu[cp] = 0;
            //PlayWAV("staredit\\wav\\se_gui_misc6.wav");
        }
    }
}

function changePokemon(cp){
    if(pokeChange[cp]){
        pokeChange[cp] += 1;
        const changeDelay = pokeChange[cp];
        const step = bbs.battleStep[cp];
        if(changeDelay == 2){
            if(step != 7){
                MoveLocation(vli.tempMoveLoc, ppst.playerPokemonUnit[cp], cp, bbs.battleLocation[cp]);
                fc.scanEffect(vli.tempMoveLoc, 980);
            }
            PlayWAV("staredit\\wav\\emerald_ball throw.wav");
        }
        if(changeDelay < 24*2){
            t.insert(0, "\n\n\n");
            t.appendf("\x13{:s}\n",vms.sph1);
            t.appendf("\x13\x04돌아와! \x17{:s}!\n",bbs.pokemonName[cp]);
            t.appendf("\x13\x04가랏! \x1F{:s}!\n",changeName[cp]);
            t.appendf("\x13{:s}",vms.sph1);
            t.DisplayAt(0);
            if(step != 7){
                MoveLocation(vli.tempMoveLoc, ppst.playerPokemonUnit[cp], cp, bbs.battleLocation[cp]);
                Order(ppst.playerPokemonUnit[cp], cp, bbs.battleLocation[cp], Move, vli.tempMoveLoc);
            }
        }
        else{
            const cpPk = cp*pmp.userPokemonNum;
            const pkNumber = iug.subMiniMenu[cp]-1; //교체할 포켓몬의 볼번호
            const preNumber = bbs.pokemonNumber[cp];
            if(step != 7){
                //현재 전투중인 포켓몬 설정
                const mpk = pmp.myPokemon.cast(pmp.playerPokemonList[cpPk+preNumber]);
                mpk.hitpoint = bbg.pokemonCurHP[cp];
            }

            //교체대기 중인 포켓몬 설정
            bbs.pokemonNumber[cp] = pkNumber;
            const pUnit = ppst.settingPokemon(cp, true, pkNumber);

            const pk = ppi.Pokemon1.cast(ppi.pokemonList[bbs.pokemonIndex[cp]-1]);
            const ck = ppi.Pokemon1.cast(ppi.pokemonList[bbs.pokemonIndex[cp+4]-1]);
            pwr.calculateWeak_PVE(cp, pk.type, ck.type);
            MoveLocation(vli.tempMoveLoc, 50, P5, bbs.battleLocation[cp]); //내 포켓몬
            RemoveUnitAt(1, ppst.playerPokemonUnit[cp], bbs.battleLocation[cp], cp);
            bbs.battleEPD[cp] = fc.SetNextUnitEPD();
            CreateUnit(1, pUnit, vli.tempMoveLoc, cp);

            //교체된 포켓몬의 체력 설정하기
            const mpk2 = pmp.myPokemon.cast(pmp.playerPokemonList[cpPk+pkNumber]);
            SetMemoryEPD(bbs.battleEPD[cp]+2, SetTo, mpk2.hitpoint*256);
            fc.scanEffect(vli.tempMoveLoc, 391);
            PlayWAV("staredit\\wav\\Battle recall.wav");

            iug.subMiniMenu[cp] = 0;
            pokeChange[cp] = 0;
            if(step == 7){bbs.battleStep[cp] = 6;}
        }
    }
}

function subMenu_PokemonInfo(cp, height){
    if(pps.guiUpdate[cp]){
        pps.guiUpdate[cp] = 0;
        const pokeIndex = iug.subMiniMenu[cp]-1;
        const cpPk = cp*pmp.userPokemonNum;
        const mpk = pmp.myPokemon.cast(pmp.playerPokemonList[cpPk+pokeIndex]);

        const pk = ppi.Pokemon1.cast(ppi.pokemonList[mpk.index-1]);
        const pkn = ppi.Pokemon2.cast(pk.next);
        const type1, type2 = pcm.divideType(pk.type);

        t.insert(0);
        t.appendf("\t\t\x17Lv.{} \x04{:s} \x04({:s}{:s}\x04)\n\n",mpk.level,pk.name,pwr.typeText[type1],pwr.typeText[type2]);
        if(pokePage[cp] == 0){
            const mpkn = pmp.myPokemon2.cast(mpk.next);
            const requireExp = EUDArray.cast(dT.expList[pkn.expType]);

            const hp = pcm.returnStatsHP(pk.BTs);
            const vhp = pcm.returnIVsHP(mpk.IVs);
            const unitHP = pcm.returnHitPoints(cp, mpk.level, hp, vhp);

            t.appendf("\t\t\x08HP \x04{}/{}",mpk.hitpoint, unitHP);
            if(mpk.state == 0){t.appendf("\n");}
            else{t.appendf(" \x08(기절)\n");}
            t.appendf("\t\t\x1FEXP \x04{}/{}\n",mpk.ownEXP,requireExp[mpk.level]);
            t.appendf("\t\t\x07친밀도 \x04: {}\n", mpkn.friendship);
            t.appendf("\t\t\x11지닌물건\x1E(F) \x04: ");
            if(mpk.item > 0){
                const pkItem = iif.Item.cast(iif.itemList[mpk.item-1]);
                t.appendf("{:s}\n", pkItem.name);
            }
            else{t.appendf("없음\n");}
        }
        else if(pokePage[cp] == 1){
            const hp, atk, amr, spd = pcm.divideStats(pk.BTs);
            const vhp,vatk,vamr,vspd= pcm.divideIVs(mpk.IVs);

            const unitHP = pcm.returnHitPoints(cp, mpk.level, hp, vhp);
            const unitAtk = pcm.returnAttacks(cp, mpk.level, atk, vatk);
            const unitArmor = pcm.returnArmor(cp, mpk.level, amr, vamr);
            const unitSpeed = pcm.returnSpeed(cp, mpk.level, spd, vspd);

            t.appendf("\t\t\x08능력치\n");
            t.appendf("\t\t\x04체력 \x1E{} \x04공격력 \x1E{}\n",unitHP,unitAtk);
            t.appendf("\t\t\x04방어 \x1E{} \x04스피드 \x1E{}\n",unitArmor,unitSpeed);
            t.appendf("\t\t\x04\n");
        }
        t.appendf("\t\t\x17순서교체 (D)\n\t\t\x1E뒤로가기 (A)");
    }
    t.DisplayAt(height);
    eprintf("{:s}",vms.stickText2);

    //PageDown
    if(Deaths(cp, Exactly, 32, "A Stick")){
        pokePage[cp] += 1;
        if(pokePage[cp] == 2){pokePage[cp] = 0;}
        PlayWAV("staredit\\wav\\GUI sel cursor.wav");
        pps.guiUpdate[cp] = 1;
    }
    //뒤로가기
    if(Deaths(cp, Exactly, 4, "A Stick")){
        iug.subMiniMenu[cp] = 0;
        pps.guiUpdate[cp] = 1;
        PlayWAV("staredit\\wav\\se_gui_misc5.wav");
    }
    //순서교체하기
    if(Deaths(cp, Exactly, 8, "A Stick")){
        const pokemonNum = pmp.returnPokemonNumber(cp);
        if(pokemonNum > 1){
            pokeAction[cp] = 2;
            pps.guiUpdate[cp] = 1;
            PlayWAV("staredit\\wav\\se_gui_misc6.wav");
        }
    }
    //지닌물건 회수하기
    if(Deaths(cp, Exactly, 16, "A Key") && pokePage[cp] == 0){
        const pokeIndex = iug.subMiniMenu[cp]-1;
        const cpPk = cp*pmp.userPokemonNum;
        const mpk = pmp.myPokemon.cast(pmp.playerPokemonList[cpPk+pokeIndex]);
        //지닌 아이템이 있는 경우
        if(mpk.item){
            const preItem = mpk.item;
            mpk.item = 0;
            imb.addItem(cp, preItem-1, 1, false);
            const pkItem = iif.Item.cast(iif.itemList[preItem-1]);
            t.printf("\x07！\x04포켓몬이 지닌 \x1D{:s}\x04을(를) 회수했습니다.",pkItem.name);
            pps.guiUpdate[cp] = 1;
            PlayWAV("staredit\\wav\\se_gui_misc6.wav");
        }
    }
}