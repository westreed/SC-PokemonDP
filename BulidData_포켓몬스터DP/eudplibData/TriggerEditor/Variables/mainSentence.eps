import functions as fc;
import Battle.battleStart as bbs;
import Interface.menu_Book as imk;
import Interface.menu_Bag as imb;
import Player.movementUnit as pmu;
import Player.playerSetting as pps;
import Player.playerUnit as ppu;
import Player.encounterTrainer as pet;
import Variables.locationInfo as vli;
import Variables.npcInfo as vni;
import Variables.sentenceLog as vsl;
import Variables.etcData as ved;
import Variables.subSentence as vus;
import Variables.restrictSentence as vrs;
import BGMPlayer as BGM;

const s = StringBuffer(1024);
const t = StringBuffer(1024);
const actionStep    = PVariable();
const restoreMusic  = PVariable();
const sphIndex      = PVariable();
const sphDelay      = PVariable();
const sphTimer      = PVariable();

const action        = 0;
const npcNum        = 4; //메인스토리 진행 중 움직이는 NPC 관리용
const npcEPD        = EUDArray(4*npcNum);

const sph1          = Db("\x04────────────────");
const sph2          = Db("\x04──────────\x11다음 F키\x04──");
const sph3          = Db("\n\n\n\x13\x11｜\n\x13\x11·");
const stickText     = Db("\x11[ \x04D 선택 \x11]  \x1F[ \x04WS 메뉴이동\x1F ]  \x08[ \x04A 취소/뒤로\x08 ]");
const stickText2    = Db("\x11[ \x04D 교체 \x11]  \x1F[ \x04E 다음페이지\x1F ]  \x1C[ \x04F 지닌물건해제 \x1C]  \x08[ \x04A 뒤로\x08 ]");
const stickText3    = Db("\x11[ \x04D 선택 \x11]  \x1F[ \x04WS 메뉴이동\x1F ]  \x1D[ \x04QE 페이지이동\x1D ]  \x08[ \x04A 취소/뒤로\x08 ]");
const stickText4    = Db("\x11[ \x04D 선택 \x11]  \x08[ \x04A 취소/뒤로\x08 ]");
const stickText5    = Db("\x08[ \x04A 취소/뒤로\x08 ]");

function removeText(){
    t.printfAt(0, "\n\n\n\n\n\n\n\n");
}

function removeTextAll(){
    t.printfAt(0, "\n\n\n\n\n\n\n\n\n\n");
}

function nextStep(cp,type){
    if(type == 0){actionStep[cp] += 1;}
    else if(type == 1){vrs.restrictStep[cp] += 1;}
    else if(type == 2){vus.subStep[cp] += 1;}
    else if(type == 3){pet.tnStep[cp] += 1;}
}

function endStep(cp,type){
    if(type == 0){actionStep[cp] = 0; pps.mainStory[cp] += 1;}
    else if(type == 1){vrs.restrictStep[cp] = 0; vrs.restrictIndex[cp] = 0;}
    else if(type == 2){vus.subStep[cp] = 0; vus.subIndex[cp] = 0;}
    else if(type == 3){pet.tnStep[cp] = 0;}
}

function moveLocationPlayer(cp, loc, x, y){
    MoveLocation(loc, pps.playerUnit[cp], cp, vli.moveLoc+cp);
    addloc(loc, x, y);
    RemoveUnitAt(1, ved.junkUnit, loc, 11);
}

function createNPC(cp, tp:TrgPlayer, loc:TrgLocation, npc, slot){
    const cpi = cp*npcNum;
    const epd = fc.SetNextUnitEPD();
    CreateUnit(1, npc, loc, tp);
    SetMemoryXEPD(epd+0xDC/4, SetTo, 0x10A00000, 0x10A00000); //발업+isGathering+NoCoilde
    npcEPD[cpi+slot] = epd;
}

function resetNPC(cp){
    const cpi = cp*npcNum;
    for(var i=0; i<npcNum; i++){npcEPD[cpi+i] = 0;}
}

//상시발동 트리거 : removeTimer가 1일 때 삭제시키는 함수
function removeNPC(cp){
    const cpi = cp*npcNum;
    for(var i=0; i<npcNum; i++){
        if(MemoryEPD(EPD(npcEPD)+cpi+i, AtLeast, 1)){
            const epd = npcEPD[cpi+i];
            if(MemoryXEPD(epd+0x110/4, Exactly, 0x1, 0xFFFF)){
                SetMemoryXEPD(epd+0x64/4, SetTo, ved.junkUnit, 0xFFFF);
                RemoveUnit(ved.junkUnit, maskread_epd(epd+0x4C/4, 0xF));
                npcEPD[cpi+i] = 0;
            }
        }
    }
}

//npc Index -> npc EPD
function npcIndex(cp, index){
    return npcEPD[cp*npcNum+index];
}

//느낌표 텍스트
function bangText(cp, loc:TrgLocation){
    CenterView(loc);
    t.printfAt(0,"{:s}", sph3);
    if(sphDelay[cp] < 24*2){sphDelay[cp] += 1;}
    else{sphDelay[cp] = 0; nextStep(cp,action);}
}

function speechText(cp, loc:TrgLocation, log:EUDArray, type, state){
    CenterView(loc);
    const name = sphIndex[cp]*2;
    const index = name+1;
    t.insert(0, "\n\n");
    t.appendf("\x17{:s}\n", vni.npcName[log[name]]);
    t.appendf("{:s}\n", sph1);
    t.appendf("\n\n");
    t.appendf("{:s}", sph2);
    t.DisplayAt(0);
    const tecolor = 4, 2, 0x1E, 5, 0;
    if(!s.fadeIn(ptr2s(log[index]), color=tecolor, wait=1, line=4, tag=py_str("log"))){
        s.printfAt(4,"{:s}",log[index]);
    }
    if(Deaths(cp, Exactly, 16, "A Key")){
        if(log[index+2] != 0){
            if(IsUserCP()){TextFX_SetTimer("log", SetTo, 0);}
            sphIndex[cp]+= 1;
            PlayWAV("staredit\\wav\\GUI sel cursor.wav");
        } else {
            sphIndex[cp] = 0; sphDelay[cp] = 0;
            nextStep(cp,type);
            t.printAt(0, "\n\n\n\n\n\n\n");
            pmu.changeMoveState(cp, state);
        }
    }
}

function waitSpeechText(cp, loc:TrgLocation, log:EUDArray, type, state){
    if(sphTimer[cp] > 1){sphTimer[cp] --;}
    CenterView(loc);
    const name = sphIndex[cp]*3;
    const timer = name+1;
    const index = name+2;
    t.insert(0, "\n\n");
    t.appendf("\x17{:s}\n", vni.npcName[log[name]]);
    t.appendf("{:s}\n", sph1);
    t.appendf("\n\n");
    t.appendf("{:s}", sph2);
    t.DisplayAt(0);
    const tecolor = 4, 2, 0x1E, 5, 0;
    if(!s.fadeIn(ptr2s(log[index]), color=tecolor, wait=1, line=4, tag=py_str("log"))){
        s.printfAt(4,"{:s}",log[index]);
    }
    if((Deaths(cp, Exactly, 16, "A Key") && sphTimer[cp] == 0) || sphTimer[cp] == 1){
        sphTimer[cp] = log[timer+3];
        if(log[index+3] != 0){
            if(IsUserCP()){TextFX_SetTimer("log", SetTo, 0);}
            sphIndex[cp]+= 1;
            PlayWAV("staredit\\wav\\GUI sel cursor.wav");
        } else {
            sphIndex[cp] = 0; sphDelay[cp] = 0;
            nextStep(cp,type);
            t.printAt(0, "\n\n\n\n\n\n\n");
            pmu.changeMoveState(cp, state);
        }
    }
}

//메인시나리오 진행
function speechSystem(cp){
    removeNPC(cp);
    vrs.restrictAction(cp);
    vus.subSpeech(cp);
    if(vus.subIndex[cp]){return;}
    if(vrs.restrictIndex[cp]){return;}
    const main = pps.mainStory[cp];
    const step = actionStep[cp];
    if(main == 1){
        t.insert(0);
        t.appendf("\t{:s}\n", sph1);
        t.appendf("\t\t\x04결국 수색대의\n");
        t.appendf("\t\t\x04노력에도 불구하고\n");
        t.appendf("\t\t\x04색깔이 다른 희귀한 포켓몬\n");
        t.appendf("\t\t\x04빨간 갸라도스의 모습은\n");
        t.appendf("\t\t\x04목격조차 한 번\n");
        t.appendf("\t\t\x04할 수 없었습니다...\n");
        t.appendf("\t{:s}", sph1);
        t.DisplayAt(0);
        if(sphDelay[cp] < 480){sphDelay[cp] += 1;}
        else{sphDelay[cp] = 0; pps.mainStory[cp] += 1;}
    }
    else if(main == 2){
        removeText();
        // SetDeaths(cp, SetTo, 31, "A Light");
        pps.userSetLight[cp] = 31;
        pps.settingMusic[cp] = 4;
        ppu.createPlayer(cp);
        pps.mainStory[cp] += 1;
        if(IsUserCP()){SetVariables(vli.locationText,1);}
    }
    //집에서 엄마가 용식과의 약속에 관한 이야기를 해줌.
    else if(main == 3){
        if(step == 0){
            if(Bring(cp, Exactly, 1, pps.playerUnit[cp], "[Story]1")){
                PlayWAV("staredit\\wav\\se_gui_choose.wav");
                MoveLocation(vli.npcLoc+cp, vni.medic, P5, vli.mapLoc[0]);
                pmu.changeMoveState(cp, false);
                nextStep(cp,action);
            }
        }else if(step == 1){
            bangText(cp, vli.npcLoc+cp);
        }else if(step == 2){
            speechText(cp, vli.npcLoc+cp, vsl.storyLog1, action, true);
        }else{endStep(cp,action);}
    }
    //용식이의 집 앞에서 대화
    else if(main == 4){
        if(step == 0){
            if(Deaths(cp, Exactly, 16, "A Key") && Bring(cp, Exactly, 1, pps.playerUnit[cp], "[Story]2")){
                PlayWAV("staredit\\wav\\Door enter.wav");
                restoreMusic[cp] = pps.settingMusic[cp];
                pps.settingMusic[cp] = 7;
                createNPC(cp, P6, "[Story]2", vni.rival, 0);
                moveLocationPlayer(cp, vli.tempMoveLoc, 0, 32); //플레이어기준으로 로케이션 이동
                pmu.changeMoveState(cp, false);
                Order(pps.playerUnit[cp], cp, 64, Move, vli.tempMoveLoc);
                nextStep(cp,action);
            }
        }
        else if(step == 1){
            bangText(cp, "[Story]2");
        }else if(step == 2){
            speechText(cp, "[Story]2", vsl.storyLog2, action, true);
        }else if(step == 3){
            pps.settingMusic[cp] = restoreMusic[cp];
            fc.COrderLocEPD(npcIndex(cp,0), 6, vli.mapLoc[2]); //6:move
            SetMemoryXEPD(npcIndex(cp,0)+0x110/4, SetTo, 0x32, 0xFFFF);
            endStep(cp,action);
        }
    }
    //201번 도로로 나갔을 때, 용식이와 대화
    else if(main == 5){
        if(step == 0){
            if(Bring(cp, Exactly, 1, pps.playerUnit[cp], "[Story]3")){
                PlayWAV("staredit\\wav\\se_gui_choose.wav");
                moveLocationPlayer(cp, vli.tempMoveLoc, 0, -32); //플레이어기준으로 로케이션 이동
                createNPC(cp, P6, vli.tempMoveLoc, vni.rival, 0);
                pmu.changeMoveState(cp, false);
                nextStep(cp,action);
            }
        }else if(step == 1){
            speechText(cp, "[Story]3", vsl.storyLog3, action, true);
        }else if(step == 2){
            fc.COrderLocEPD(npcIndex(cp,0), 6, vli.mapLoc[3]); //6:move
            SetMemoryXEPD(npcIndex(cp,0)+0x110/4, SetTo, 0x32, 0xFFFF);
            endStep(cp,action);
        }
    }
    //진실호수 근처에서 마박사 일행과 조우
    else if(main == 6){
        if(step == 0){
            if(Bring(cp, Exactly, 1, pps.playerUnit[cp], "[Story]4")){
                PlayWAV("staredit\\wav\\se_gui_choose.wav");
                moveLocationPlayer(cp, vli.tempMoveLoc, 0, -32); //플레이어기준으로 로케이션 이동
                createNPC(cp, P6, vli.tempMoveLoc, vni.rival, 0);
                MoveLocation(vli.tempMoveLoc, ved.junkUnit, 11, "[Story]5");
                addloc(vli.tempMoveLoc, 0, 16);
                createNPC(cp, P7, vli.tempMoveLoc, vni.doctor1, 1); //마박사
                addloc(vli.tempMoveLoc, 16, 0);
                createNPC(cp, P6, vli.tempMoveLoc, vni.friend, 2); //빛나
                pmu.changeMoveState(cp, false);
                nextStep(cp,action);
            }
        }else if(step == 1){
            speechText(cp, "[Story]4", vsl.storyLog5, action, false);
        }else if(step == 2){
            endStep(cp,action);
        }
    }
    else if(main == 7){
        if(step == 0){
            MoveLocation(vli.npcLoc+cp, ved.junkUnit, 11, "[Story]4");
            CreateUnit(1, ved.visionUnit, "[Story]5", cp); //시야 생성
            CreateUnit(1, ved.screenUnit, "[Story]4", cp);
            nextStep(cp,action);
        }else if(step == 1){
            Order(ved.screenUnit, cp, 64, Move, "[Story]5");
            const nLoc = vli.npcLoc+cp;
            MoveLocation(nLoc, ved.screenUnit, cp, 64);
            CenterView(nLoc);
            if(Bring(cp, Exactly, 1, ved.screenUnit, "[Story]5")){
                RemoveUnit(ved.screenUnit, cp);
                endStep(cp,action);
            }
        }
    }
    //마박사와 빛나 대화 후 가방에서 포켓몬을 뽑고 전투
    else if(main == 8){
        if(step == 0){
            speechText(cp, "[Story]5", vsl.storyLog6, action, false);
        }else if(step == 1){
            RemoveUnit(ved.visionUnit, cp); //시야 삭제
            fc.COrderLocEPD(npcIndex(cp,1), 6, vli.mapLoc[2]); //6:move
            fc.COrderLocEPD(npcIndex(cp,2), 6, vli.mapLoc[2]); //6:move
            SetMemoryXEPD(npcIndex(cp,1)+0x110/4, SetTo, 0x40, 0xFFFF);
            SetMemoryXEPD(npcIndex(cp,2)+0x110/4, SetTo, 0x40, 0xFFFF);
            nextStep(cp,action);
        }else if(step == 2){
            if(sphDelay[cp] < 24*3){sphDelay[cp]++;}
            else{
                sphDelay[cp] = 0;
                nextStep(cp,action);
            }
        }else if(step == 3){
            speechText(cp, "[Story]4", vsl.storyLog7, action, false);
        }else if(step == 4){
            SetMemoryXEPD(npcIndex(cp,0)+0x110/4, SetTo, 0x2, 0xFFFF);
            Order(pps.playerUnit[cp], cp, 64, Move, "[Story]5");
            nextStep(cp,action);
        }else if(step == 5){
            if(Bring(cp, Exactly, 1, pps.playerUnit[cp], "[Story]5")){
                Order(pps.playerUnit[cp], cp, 64, Move, vli.moveLoc+cp);
                moveLocationPlayer(cp, vli.tempMoveLoc, 32, 0); //플레이어기준으로 로케이션 이동
                createNPC(cp, P6, vli.tempMoveLoc, vni.rival, 0);
                nextStep(cp,action);
            }
        }else if(step == 6){
            speechText(cp, "[Story]5", vsl.storyLog8, action, false);
        }else if(step == 7){
            SetMemoryXEPD(npcIndex(cp,0)+0x110/4, SetTo, 0x2, 0xFFFF);
            bbs.wildSetting(cp, 0, 9, 2);
            endStep(cp,action);
        }
    }
    //배틀 종료 후 대화
    else if(main == 10){
        if(step == 0){
            moveLocationPlayer(cp, vli.tempMoveLoc, 32, 0); //플레이어기준으로 로케이션 이동
            createNPC(cp, P6, vli.tempMoveLoc, vni.rival, 0);
            pmu.changeMoveState(cp, false);
            nextStep(cp,action);
        }else if(step == 1){
            speechText(cp, vli.moveLoc+cp, vsl.storyLog9, action, false);
        }else if(step == 2){
            createNPC(cp, P6, "[Story]4", vni.friend, 1); //빛나
            moveLocationPlayer(cp, vli.tempMoveLoc, 0, 32);
            fc.COrderLocEPD(npcIndex(cp,1), 6, vli.tempMoveLoc); //6:move
            nextStep(cp,action);
        }else if(step == 3){
            const epd = npcIndex(cp,1);
            if(fc.BringEPD(epd, vli.moveLoc+cp)){
                SetMemoryXEPD(epd+0x4C/4, SetTo, 0x100, 0xFF00); //Stop
                restoreMusic[cp] = pps.settingMusic[cp];
                pps.settingMusic[cp] = 17;
                BGM.instantPlay();
                nextStep(cp,action);
            }
        }else if(step == 4){
            speechText(cp, vli.moveLoc+cp, vsl.storyLog10, action, false);
        }else if(step == 5){
            fc.COrderLocEPD(npcIndex(cp,1), 6, vli.mapLoc[2]); //6:move
            SetMemoryXEPD(npcIndex(cp,1)+0x110/4, SetTo, 0x40, 0xFFFF);
            pps.settingMusic[cp] = restoreMusic[cp];
            nextStep(cp,action);
        }else if(step == 6){
            speechText(cp, vli.moveLoc+cp, vsl.storyLog11, action, true);
        }else if(step == 7){
            SetMemoryXEPD(npcIndex(cp,0)+0x110/4, SetTo, 0x2, 0xFFFF);
            endStep(cp,action);
        }
    }
    //떡잎마을 앞에서 마박사일행과 조우
    else if(main == 11){
        if(step == 0){
            if(Bring(cp, Exactly, 1, pps.playerUnit[cp], "[Story]3")){
                PlayWAV("staredit\\wav\\se_gui_choose.wav");
                pmu.changeMoveState(cp, false);
                MoveLocation(vli.tempMoveLoc, pps.playerUnit[cp], cp, 64);
                addloc(vli.tempMoveLoc, -16, 0);
                createNPC(cp, P6, vli.tempMoveLoc, vni.rival, 0); //용식
                MoveLocation(vli.tempMoveLoc, ved.junkUnit, 11, "[Story]3");
                createNPC(cp, P7, vli.tempMoveLoc, vni.doctor1, 1); //마박사
                addloc(vli.tempMoveLoc, 0, 16);
                createNPC(cp, P6, vli.tempMoveLoc, vni.friend, 2); //빛나
                nextStep(cp,action);
            }
        }else if(step == 1){
            CenterView(vli.moveLoc+cp);
            t.printfAt(0,"{:s}", sph3);
            if(sphDelay[cp] < 24*2){sphDelay[cp] += 1;}
            else{sphDelay[cp] = 0; nextStep(cp,action);}
        }else if(step == 2){
            speechText(cp, "[Story]3", vsl.storyLog12, action, false);
            if(sphDelay[cp] == 0 && sphIndex[cp] == 8){
                sphDelay[cp] += 1;
                fc.COrderLocEPD(npcIndex(cp,1), 6, vli.mapLoc[4]); //6:move
                SetMemoryXEPD(npcIndex(cp,1)+0x110/4, SetTo, 0x40, 0xFFFF);
            }else if(sphDelay[cp] == 1 && sphIndex[cp] == 11){
                sphDelay[cp] += 1;
                fc.COrderLocEPD(npcIndex(cp,2), 6, vli.mapLoc[4]); //6:move
                SetMemoryXEPD(npcIndex(cp,2)+0x110/4, SetTo, 0x40, 0xFFFF);
            }
        }else if(step == 3){
            if(sphDelay[cp] == 1){SetMemoryXEPD(npcIndex(cp,0)+0x110/4, SetTo, 0x2, 0xFFFF); pps.userSetLight[cp] = 0;}
            if(sphDelay[cp] < 24*2){sphDelay[cp] += 1;}
            else{sphDelay[cp] = 0; nextStep(cp,action);}
        }else if(step == 4){
            if(sphDelay[cp] == 1){
                pps.userSetLight[cp] = 31;
                MoveLocation(vli.npcLoc+cp, vni.medic, P5, vli.mapLoc[0]);
                addloc(vli.npcLoc+cp, 32, 0);
                MoveUnit(1, pps.playerUnit[cp], cp, 64, vli.npcLoc+cp);
                CenterView(vli.npcLoc+cp);
            }
            if(sphDelay[cp] < 24*2){sphDelay[cp] += 1;}
            else{sphDelay[cp] = 0; nextStep(cp,action);}
        }else if(step == 5){
            speechText(cp, vli.npcLoc+cp, vsl.storyLog13, action, true);
            if(sphDelay[cp] == 0 && sphIndex[cp] == 11){
                sphDelay[cp] += 1;
                PlayWAV("staredit\\wav\\ObtainedanItem.wav");
            }
        }else{
            SetMemoryXEPD(ppu.playerEPD[cp]+0xDC/4, SetTo, 0x10000000, 0x10000000); //스피드업
            endStep(cp,action);
        }
    }
    //잔모래마을에서 빛나와 마주 (도감 획득)
    else if(main == 12){
        if(step == 0){
            if(Bring(cp, Exactly, 1, pps.playerUnit[cp], "[Story]6")){
                PlayWAV("staredit\\wav\\se_gui_choose.wav");
                pmu.changeMoveState(cp, false);
                MoveLocation(vli.tempMoveLoc, ved.junkUnit, 11, "[Story]6");
                createNPC(cp, P6, vli.tempMoveLoc, vni.friend, 0); //빛나
                addloc(vli.tempMoveLoc, -32, 0);
                Order(pps.playerUnit[cp], cp, 64, Move, vli.tempMoveLoc);
                nextStep(cp,action);
            }
        }else if(step == 1){
            bangText(cp, vli.moveLoc+cp);
        }else if(step == 2){
            speechText(cp, vli.moveLoc+cp, vsl.storyLog14, action, false);
            if(sphDelay[cp] == 0 && sphIndex[cp] == 4){
                sphDelay[cp] += 1;
                MoveLocation(vli.tempMoveLoc, 50, P5, vli.mapLoc[4]);
                createNPC(cp, P6, vli.tempMoveLoc, vni.rival, 1); //용식
                PlayWAV("staredit\\wav\\Door enter.wav");
            }
            if(sphDelay[cp] == 1 && sphIndex[cp] == 7){
                sphDelay[cp] += 1;
                fc.COrderLocEPD(npcIndex(cp,1), 6, vli.mapLoc[6]); //6:move
                SetMemoryXEPD(npcIndex(cp,1)+0x110/4, SetTo, 0x40, 0xFFFF);
            }
        }else if(step == 3){
            if(sphDelay[cp] == 1){SetMemoryXEPD(npcIndex(cp,0)+0x110/4, SetTo, 0x2, 0xFFFF); pps.userSetLight[cp] = 0;}
            if(sphDelay[cp] < 24*2){sphDelay[cp] += 1;}
            else{sphDelay[cp] = 0; nextStep(cp,action);}
        }else if(step == 4){
            if(sphDelay[cp] == 1){
                const nLoc = vli.npcLoc+cp;
                pps.userSetLight[cp] = 31;
                MoveLocation(nLoc, vni.doctor1, P5, vli.mapLoc[7]);
                addloc(nLoc, 0, 32);
                MoveUnit(1, pps.playerUnit[cp], cp, 64, nLoc);
                CenterView(nLoc);
                addloc(nLoc, 32, 0);
                createNPC(cp, P6, nLoc, vni.friend, 0); //빛나
            }
            if(sphDelay[cp] < 24*2){sphDelay[cp] += 1;}
            else{sphDelay[cp] = 0; nextStep(cp,action);}
        }else if(step == 5){
            speechText(cp, vli.npcLoc+cp, vsl.storyLog15, action, true);
            if(sphDelay[cp] == 0 && sphIndex[cp] == 25){
                sphDelay[cp] += 1;
                PlayWAV("staredit\\wav\\ObtainedanItem.wav");
            }
        }else{
            SetMemoryXEPD(npcIndex(cp,0)+0x110/4, SetTo, 0x2, 0xFFFF);
            endStep(cp,action);
            imk.writeSinoBook(cp, pps.setStartingPokemon[cp]-1);
        }
    }
    //마박사에게 도감을 받고나서 엄마에게 모험에 대한 이야기하기
    else if(main == 13){
        if(step == 0){
            if(Bring(cp, Exactly, 1, pps.playerUnit[cp], "[Map]광휘의집")){
                PlayWAV("staredit\\wav\\se_gui_choose.wav");
                MoveLocation(vli.npcLoc+cp, vni.medic, P5, vli.mapLoc[0]);
                pmu.changeMoveState(cp, false);
                MoveLocation(vli.tempMoveLoc, 46, P5, vli.mapLoc[0]);
                Order(pps.playerUnit[cp], cp, 64, Move, vli.tempMoveLoc);
                nextStep(cp,action);
            }
        }else if(step == 1){
            bangText(cp, vli.npcLoc+cp);
        }else if(step == 2){
            speechText(cp, vli.npcLoc+cp, vsl.storyLog16, action, false);
        }else if(step == 3){
            MoveLocation(vli.tempMoveLoc, 50, P5, vli.mapLoc[0]);
            createNPC(cp, P6, vli.tempMoveLoc, vni.medic, 0); //용식엄마
            MoveLocation(vli.tempMoveLoc, vni.medic, P5, vli.mapLoc[0]);
            addloc(vli.tempMoveLoc, 0, 32);
            fc.COrderLocEPD(npcIndex(cp,0), 6, vli.tempMoveLoc); //6:move
            nextStep(cp,action);
        }else if(step == 4){
            speechText(cp, vli.npcLoc+cp, vsl.storyLog17, action, true);
            if(sphDelay[cp] == 0 && sphIndex[cp] == 10){
                sphDelay[cp] += 1;
                imb.addItem(cp, 56, 1, false);
                PlayWAV("staredit\\wav\\ObtainedanItem.wav");
            }
        }else{
            SetMemoryXEPD(npcIndex(cp,0)+0x110/4, SetTo, 0x2, 0xFFFF);
            endStep(cp, action);
        }
    }
}